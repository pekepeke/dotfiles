#! /usr/bin/env perl

use strict;
use warnings;
use DBI;
use Getopt::Long;
use IO::Handle;
use JSON qw(to_json);
use Time::HiRes qw(sleep);

our $dbi;
our $user = 'root';
our $password;
our $host;
our $port;
our $mysql_socket;

our $samples = 1000;
our $interval = 0.1;
our $help;

GetOptions(
    'dbi=s'      => \$dbi,
    'user=s'     => \$user,
    'password=s' => \$password,
    'host=s'     => \$host,
    'port=i'     => \$port,
    'socket=s'   => \$mysql_socket,
    'interval=f' => \$interval,
    'samples=i'  => \$samples,
    help         => \$help,
) or exit 1;

if ($help) {
    print << "EOT";

Usage:   $0 options

Options:
    --host=      address of MySQL server
    --port=      port no. of MySQL server
    --socket=    unix socket of MySQL server
        (by default, connects to the server specified in my.cnf)

    --user=      MySQL username (default: root)
    --password=  MySQL password (default: no password)

    --interval=  interval between samples (default: 0.1sec)
    --samples=   number of samples to take (default: 1000, infinite if set
                 to 0)

    --help       print this help

EOT
;
    exit(0);
}

unless (defined $dbi) {
    $dbi = 'DBI:mysql(mysql_enable_utf8=>1):database=mysql';
    for my $n (qw/user password host port mysql_socket/) {
	no strict qw(refs);
	$dbi .= ";$n=$$n"
	    if $$n;
    }
}

my $dbh = DBI->connect($dbi)
    or die DBI->errstr;

for (my $i = 0; $i < $samples; $i++) {
    my $rows = $dbh->selectall_arrayref(
        'SHOW FULL PROCESSLIST',
        { Slice => {} },
    ) or die $dbh->errstr;
    print to_json(
        [ grep {
            ($_->{Command} || '') eq 'Query'
                and ($_->{Info} || '') ne 'SHOW FULL PROCESSLIST'
        } @$rows ],
        { ascii => 1 },
    ), "\n";
    sleep($interval);
}
